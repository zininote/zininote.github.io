---
layout: post
title: "INDEX, MATCH: VLOOKUP 의 대안, INDEX-MATCH 함수"
updated: 2021-01-16
tags: [msoffice,excel]
---

## VLOOKUP 함수의 단점

VLOOKUP 함수는 엑셀에서 상당히 자주 사용되는 편리한 함수이지만, 그만큼 아쉬운 점도 많은 함수다. 대체로 아래와 같은 단점들이 있다.

- 검색기준열은 검색범위의 가장 왼쪽열로 고정
- 두가지 이상 검색조건 사용이 어려움
- 면에서의 검색(VLOOKUP 과 HLOOKUP 의 혼합)이 어려움
- 검색범위 안에 열삽입/삭제를 하면 검색결과가 틀어질 가능성이 높음
- 영문 대/소문자 구별 불가능

INDEX-MATCH 함수 조합을 사용하면 위 단점들 없이, VLOOKUP 함수와 동일한 결과를 나타내도록 할 수 있다.

## 기본 사용법

```excel
= VLOOKUP(검색값, 검색범위, 열번호, FALSE)
= INDEX(검색대상열, MATCH(검색값, 검색기준열, 0))
```
{:.excel}

![그림00](/img/msoffice/excel-1801-01-01-00.png)

VLOOKUP 함수는 `검색범위`를 지정하면, `검색범위`의 가장 왼쪽열을 `검색기준열`로 삼고, 오른쪽으로 `열번호`만큼 떨어진 열을 `검색대상열`로 삼는다. 하지만 INDEX-MATCH 함수 조합은 `검색기준열`과 `검색대상열`을 독립적으로 지정하고 있다.

## 오른쪽 열을 기준으로 보다 왼쪽에 있는 열을 검색

위 기본 사용법 함수식을 그대로 사용하면 된다. `검색기준열`과 `검색대상열`을 직접 자유로이 지정할 수 있기에, 보다 왼쪽에 있는 열을 검색대상으로 삼을 수 있다. 이 특성으로 인해 VLOOKUP 처럼 중간 열삽입/삭제에도 결과가 달라지지 않는다.

![그림01](/img/msoffice/excel-1801-01-01-01.png)

## 두가지 이상 검색조건으로 검색

```excel
= INDEX(검색대상열, MATCH(1, (검색기준열1=검색값1)*(검색기준열2=검색값2), 0))
```
{:.excel}

반드시 **배열수식**으로 입력해야 정상작동 한다.

![그림02](/img/msoffice/excel-1801-01-01-02.png)

`(검색기준열=검색값)` 조건을 계속 * 기호로 붙여나가면 세가지 이상 검색조건을 적용하는 것도 가능하다.

각각의 `(검색기준열=검색값)` 결과는 TRUE 또는 FALSE 인데, 이 값에 사칙연산을 적용하면 엑셀은 TRUE 를 1, FALSE 를 0 으로 바꿔서 계산을 한다. 조건들이 곱셈으로 연결되어 있으므로, 모든 조건이 TRUE 가 되어야 1 이 될 수 있음을 응용한 것이다.

## 면에서 검색 (VLOOKUP 과 HLOOKUP 의 혼합)

```excel
= INDEX(검색대상범위, MATCH(세로방향검색값, 세로방향검색기준열, 0), MATCH(가로방향검색값, 가로방향검색기준행, 0))
```
{:.excel}

![그림03](/img/msoffice/excel-1801-01-01-03.png)

INDEX 안에 `검색대상범위`를 넓게 지정하였다. 그리고 MATCH 함수가 두번 사용되었다. 첫번째 MATCH 는 세로방향(열)으로, 두번째는 가로방향(행)으로 라고 이해하면 된다.

## 대/소문자 구별 검색

```excel
= INDEX(검색대상열, MATCH(TRUE, EXACT(검색값, 검색기준열), 0))
```
{:.excel}

반드시 **배열수식**으로 입력해야 정상작동 한다.

![그림04](/img/msoffice/excel-1801-01-01-04.png)

VLOOKUP 함수는 대/소문자 구별을 하지 않는다. 대/소문자 구별의 핵심은 EXACT 함수다. 본래 두 셀의 내용이 대/소문자까지도 동일한지를 점검하여 TRUE 혹은 FALSE 를 돌려주는 함수지만, 한쪽 인수에는 `검색값`을, 다른쪽에는 `검색기준열`을 넣고 배열수식으로 입력하면, 검색기준열의 모든 셀을 검색값과 일일이 비교하여 TRUE 와 FASLE 로 된 열을 반환한다. 이를 MATCH 함수가 받아서 TRUE 의 위치를 찾아내는 구조로 동작한다.

## 참고

VLOOKUP 함수의 단점을 없애는 대체수단으로, INDEX-MATCH 함수 조합을 소개하였으나, 일부는 VLOOKUP 함수의 개선으로도 해결할 수 있다. 아래 포스팅에 소개하였으니 참고하기 바란다.

- [VLOOKUP: 오른쪽열 기준으로 보다 왼쪽열 내용 검색](post/vlookup-search-left-from-right-column)
- [TBD] 두가지 이상 검색기준으로 VLOOKUP 검색